# prints out makefile for an awkweb project
# needs to be run as part of config job

END {
	if (HAS_DIED=="true") {
		exit -1
	}
	# check that some rules defined
	if (length(rules)==0) {
		die("ERROR: No rules defined in awkweb.ini")
	}

	# make renderer array
	parse_rule(renderers,p_renderers)
	
	print "#Auto generated by awkweb"
	
	# generate list of files that this rule depends on
	for (rule in rules) {
		parse_rule(rules[rule],p_rule)
		if (p_rule["type"] == "static") {
			print "SRC_" toupper(rule) "=$(shell find " rule " -type f)"
			print "BUILD_" toupper(rule) "=$(patsubst " rule "%, build%,$(SRC_" toupper(rule)"))"
		} else if (p_rule["type"] == "page" || p_rule["type"] == "") {
			find_cmd =  "SRC_" toupper(rule) "=$(shell find " rule " -false" 
			
			for (tmp_render in p_renderers) {
				find_cmd = find_cmd " -o -name *." tmp_render
			}
			find_cmd = find_cmd ")"
			print find_cmd
			print "BUILD_" toupper(rule) "=$(patsubst " rule "%, build%,$(SRC_" toupper(rule)":." tmp_render "=.html))"
		} else if (p_rule["type"] == "blogroll") {
			print "BUILD_" toupper(rule) "=build/" rule
			if (p_rule["src"]=="") {
				die("ERROR: No src for rule " rule)
			}
		
			find_cmd =  "SRC_" toupper(rule) "=$(shell find " p_rule["src"] " -false" 
			
			for (tmp_render in p_renderers) {
				find_cmd = find_cmd " -o -name *." tmp_render
			}
			find_cmd = find_cmd ")"
			print find_cmd
		} else {
			die("ERROR: Unrecognized rule type: " p_rule["type"])
		}
	}

	# ok, now print main build target:
	print ".PHONY: website"
	
	# print website target:
	website_target = "website: "

	for (rule in rules) {
		website_target = website_target " $(BUILD_" toupper(rule) ")"
	}
	print website_target

	# print clean target
	print ".PHONY: clean"
	print "clean:"
	print "\trm -r build"

	# print webserver target
	print "server: website"
	print "\tcd build && python3 -m http.server"

	# build instructions for each window again
	for (rule in rules) {
		parse_rule(rules[rule],p_rule)
		if (p_rule["type"] == "static") {
			print "build/%:  " rule "/%"
			print "\tmkdir -p $(@D)"
			print "\tcp $< $@"
		} else if (p_rule["type"] == "page" || p_rule["type"] == "") {
			for (renderer in p_renderers) {
				print "build/%.html: " rule "/%." renderer
				print "\tmkdir -p $(@D)"
				print "\t" p_renderers[renderer] " $< | bin/renderpage.sh $< $@ templates/page.html templates/base.html > $@"
			}
			
		} else if (p_rule["type"] == "blogroll") {
			print "build/" rule ": $(SRC_" toupper(rule) ")"
			print "\ttouch build/" rule
			
		} 
	}
	

}
